<html lang="en">
<head>
    <meta http-equiv="content-type" content="text/html; charset=utf-8">
    <title>Title Goes Here</title>
    <link rel="stylesheet" type="text/css" href="css/style.css"/>
</head>
<body>
<script id="fragmentComputeShader" type="x-shader/x-fragment">
    // x = iter, y = step
    // x = r & g, y = b & a
    uniform int iters;
    uniform int steps;
    uniform sampler2D lastData;
    uniform mat3 paramsX;
    uniform mat3 paramsY;
    uniform float startTime;
    uniform float deltaTime;

    varying vec3 vUv;

    const int MAX_ITERATIONS = 2048;

    const vec2 bitEnc2 = vec2(1., 255.);
    const vec4 bitEnc4 = vec4(1., 255., 65025., 16581375.);

    vec4 encodeFloatRGBA (float v) {
        vec4 enc = bitEnc4 * v;
        enc = fract(enc);
        enc -= enc.yzww * vec2(1./255., 0.).xxxy;
        return enc;
    }

    vec2 encodeFloatVec2 (float v) {
        vec2 enc = bitEnc2 * v;
        enc = fract(enc);
        enc -= enc.yx * (1./255.);
        return enc;
    }

    void main() {
        vec2 resolution = vec2(iters, steps);
        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec4 last = texture2D(lastData, uv);

        float t = startTime + deltaTime * float(int(gl_FragCoord.y));

        if (gl_FragCoord.x < 1.0 && gl_FragCoord.y < 1.0){
            gl_FragColor = vec4(1., 0., 0., 1.);
            return;
        }

        float x = t;
        float y = t;

        for (int iter = 0; iter < MAX_ITERATIONS; iter++) {
            if (iter > int(gl_FragCoord.x) || iter >= iters){
                break;
            }

            float xx = x * x;
            float yy = y * y;
            float tt = t * t;
            float xy = x * y;
            float xt = x * t;
            float yt = y * t;
            float nx = xx * paramsX[0][0] + yy * paramsX[1][0] + tt * paramsX[2][0] + xy * paramsX[0][1] + xt * paramsX[1][1] + yt * paramsX[2][1] + x * paramsX[0][2] + y * paramsX[1][2] + t * paramsX[2][2];
            float ny = xx * paramsY[0][0] + yy * paramsY[1][0] + tt * paramsY[2][0] + xy * paramsY[0][1] + xt * paramsY[1][1] + yt * paramsY[2][1] + x * paramsY[0][2] + y * paramsY[1][2] + t * paramsY[2][2];

            x = nx;
            y = ny;
        }

        // General division
        x /= 4.;
        y /= 4.;

        // Clamp to viewport where viewport is 0-10
        x = clamp(x, -5., 5.) + 5.;
        y = clamp(y, -5., 5.) + 5.;

        // Divide to pack into vec2
        x /= 10.;
        y /= 10.;

        gl_FragColor = vec4(encodeFloatVec2(x), encodeFloatVec2(y));
    }
</script>

<script id="vertexShader" type="x-shader/x-vertex">
    uniform sampler2D dataTexture;

    varying vec3 vUv;

    float decodeFloatVec2 (vec2 v) {
        return dot(v, vec2(1., 1./255.));
    }

    void main() {
        vec4 data = texture2D(dataTexture, vec2(position.x, position.y));

        float x = decodeFloatVec2(vec2(data.x, data.y));
        float y = decodeFloatVec2(vec2(data.z, data.w));

        // Unpack from (0.0, 1.0] float
        x *= 10.;
        y *= 10.;

        // Center
        x -= 5.;
        y -= 5.;

        vUv = position;
        gl_PointSize = 1.0;
        vec4 modelViewPosition = modelViewMatrix * vec4(x, y, 1., 1.);
        vec4 pos = projectionMatrix * modelViewPosition;
        gl_Position = pos;
    }

</script>

<script id="fragmentShader" type="x-shader/x-fragment">
    varying vec3 vUv;

    void main() {
        gl_FragColor = vec4(1., 1., 1., 1.);
    }
</script>

<script src="js/three.min.js" type="text/javascript"></script>
<script src="js/dat.gui.min.js" type="text/javascript"></script>
<script src="js/chanse.min.js" type="text/javascript"></script>
<script src="js/stats.min.js" type="text/javascript"></script>
<script src="js/chaos.js" type="text/javascript"></script>
</body>
</html>