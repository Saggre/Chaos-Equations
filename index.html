<html lang="en">
<head>
    <meta http-equiv="content-type" content="text/html; charset=utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no"/>
    <title>Chaos Equations | Saggre</title>
    <link rel="stylesheet" type="text/css" href="css/style.css"/>
</head>
<body>
<script id="vertexShader" type="x-shader/x-vertex">
    varying vec3 vColor;

    uniform float iters;
    uniform float steps;
    uniform mat3 px;
    uniform mat3 py;
    uniform float startTime;
    uniform float deltaTime;
    uniform float pixelRatio;
    uniform sampler2D colorTexture;

    const int MAX_ITERATIONS = 2048;

    void main() {
        int thisIter = int(position.x);
        float t = startTime + deltaTime * position.y;

        vec3 pos = vec3(t, t, t);
        vec3 ma, mb;

        for (int iter = 0; iter < MAX_ITERATIONS; iter++) {
            if (iter > thisIter){
                break;
            }

            ma = pos * pos;
            mb = pos.xxy * pos.yzz;

            pos.xy = vec2(
            ma.x * px[0][0] + ma.y * px[1][0] + ma.z * px[2][0] + mb.x * px[0][1] + mb.y * px[1][1] + mb.z * px[2][1] + pos.x * px[0][2] + pos.y * px[1][2] + pos.z * px[2][2],
            ma.x * py[0][0] + ma.y * py[1][0] + ma.z * py[2][0] + mb.x * py[0][1] + mb.y * py[1][1] + mb.z * py[2][1] + pos.x * py[0][2] + pos.y * py[1][2] + pos.z * py[2][2]
            );
        }

        gl_PointSize = 1.5 * pixelRatio;
        vec4 modelViewPosition = modelViewMatrix * vec4(pos.xy, 0., 1.);
        gl_Position = projectionMatrix * modelViewPosition;

        vColor = texture2D(colorTexture, position.xy / vec2(iters, steps)).rgb;
    }

</script>

<script id="fragmentShader" type="x-shader/x-fragment">
    varying vec3 vColor;

    void main() {
        gl_FragColor = vec4(vColor, 1.);
    }
</script>

<script src="js/three.min.js" type="text/javascript"></script>
<script src="js/dat.gui.min.js" type="text/javascript"></script>
<script src="js/chanse.min.js" type="text/javascript"></script>
<script src="js/stats.min.js" type="text/javascript"></script>
<script src="js/chaos.js" type="text/javascript"></script>
</body>
</html>